/*
 * Copyright (c) 2023, WSO2 LLC. (http://www.wso2.org)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'java'

description = 'Ballerina - Salesforce Test samples'

def tomlVersion = stripBallerinaExtensionVersion("${project.version}")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", '')
    }
}

def filePath = project.fileTree("${project.projectDir}")
def examples = filePath.matching {
    include("**/*.bal")
    exclude("**/deprecated/**/*.bal")
}

task testExamples {
    dependsOn ":${project.packageName}-ballerina:build"

    doLast {
        examples.each { example ->  executeBalCommand ("run ${example}", "${project.rootDir}") }
    }
}

task buildExamples {
    dependsOn ":${project.packageName}-ballerina:build"

    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":${project.packageName}-examples:test")) {
            buildExamples.enabled = false
        } else {
            testExamples.enabled = false
        }
    }
    doLast {
        try {
            exec {
                workingDir project.projectDir
                println("Working dir: ${workingDir}")
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'sh', "/c", "chmod +x ./build.sh && ./build.sh build ${tomlVersion} && exit %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', "-c", "chmod +x ./build.sh && ./build.sh build ${tomlVersion}"
                }
            }
        } catch (Exception e) {
            println("Example Build failed: " + e.message)
            throw e
        }
    }
}

test.dependsOn testExamples
build.dependsOn buildExamples
