/*
 * Copyright (c) 2023, WSO2 LLC. (http://www.wso2.org)
 *
 * WSO2 LLC. licenses this file to you under the Apache License,
 * Version 2.0 (the "License"); you may not use this file except
 * in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing,
 * software distributed under the License is distributed on an
 * "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY
 * KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations
 * under the License.
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'io.ballerina.plugin'
}

description = 'Ballerina - Salesforce Ballerina Connector'

def packageOrg = "ballerinax"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", '')
    }
}

ballerina {
    packageOrganization = packageOrg
    module = project.packageName
    testCoverageParam = "--code-coverage --coverage-format=xml"
    isConnector = true
}

task updateTomlFiles {
    doLast {
        def newConfig = ballerinaTomlFilePlaceHolder.text.replace('@project.version@', project.version.toString())
        newConfig = newConfig.replace('@toml.version@', tomlVersion)
        newConfig = newConfig.replace("@opencsv.version@", openCsvVersion)
        newConfig = newConfig.replace('@jackson.databind.version@', jacksonDatabindVersion)
        newConfig = newConfig.replace('@jackson.annotations.version@', jacksonAnnotationsVersion)
        newConfig = newConfig.replace('@jackson.core.version@', jacksonCoreVersion)
        newConfig = newConfig.replace('@cometd.java.client.version@', cometdJavaClientVersion)
        newConfig = newConfig.replace('@jetty.version@', jettyVersion)
        newConfig = newConfig.replace('@json.version@', jsonVersion)

        ballerinaTomlFile.text = newConfig
    }
}

configurations {
    externalJars
}

dependencies {
    externalJars(group: 'com.opencsv', name: 'opencsv', version: openCsvVersion)
    externalJars(group: 'org.cometd.java', name: 'cometd-java-client', version: cometdJavaClientVersion)
    externalJars(group: 'org.json', name: 'json', version: jsonVersion)
    externalJars(group: 'org.eclipse.jetty', name: 'jetty-client', version: jettyVersion)
    externalJars(group: 'org.eclipse.jetty', name: 'jetty-http', version: jettyVersion)
    externalJars(group: 'org.eclipse.jetty', name: 'jetty-io', version: jettyVersion)
    externalJars(group: 'org.eclipse.jetty', name: 'jetty-util', version: jettyVersion)
    externalJars(group: 'org.eclipse.jetty', name: 'jetty-util-ajax', version: jettyVersion)
    externalJars(group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: jacksonDatabindVersion)
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the toml files\" Ballerina.toml Dependencies.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the toml files' Ballerina.toml Dependencies.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${project.packageName}")
            credentials {
                username = System.getenv("publishUser")
                password = System.getenv("publishPAT")
            }
        }
    }
}

clean {
    delete 'build'
}

updateTomlFiles.dependsOn copyStdlibs
build.dependsOn ":${project.packageName}-native:build"
build.dependsOn "generatePomFileForMavenPublication"
publishToMavenLocal.dependsOn build
test.dependsOn ":${project.packageName}-native:build"
publish.dependsOn build
